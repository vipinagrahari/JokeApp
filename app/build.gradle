apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"
    defaultConfig {
        applicationId "com.udacity.gradle.builditbigger"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    productFlavors {
        paid {
            applicationId 'com.udacity.gradle.builditbigger.paid'
        }
        free {
            applicationId 'com.udacity.gradle.builditbigger.free'
        }
    }
}


task runAppEngine() {
    dependsOn ":jokeserver:appengineRun"

    project(":jokeserver").afterEvaluate { backend ->
        backend.extensions.appengine.daemon = true
    }
}
task runTests{
    dependsOn ":app:connectedCheck"

}
runTests.mustRunAfter runAppEngine


task testTask{
dependsOn runAppEngine,runTests
}

testTask.finalizedBy ":jokeserver:appengineStop"

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // Added for AdMob
    compile project(':javajokes')
    compile project(':jokeview')
    compile 'com.android.support:appcompat-v7:23.3.0'
    freeCompile 'com.google.firebase:firebase-ads:9.0.0'
    compile project(path: ':jokeserver', configuration: 'android-endpoints')
    compile 'com.android.support:multidex:1.0.0'
}
apply plugin: 'com.google.gms.google-services'


